cmake_minimum_required(VERSION 3.28)

# ------------------------------ Makefile parser ----------------------------- #
include(cmake/utils.cmake)

set(PATH_PREFIX "mcu/")

# Read Makefile generated by STM32CubeMX
file(READ "${PATH_PREFIX}Makefile" STM32CUBEMX_MAKEFILE)
# Strip line continuations
string(REGEX REPLACE "\\\\\\\n" "" STM32CUBEMX_MAKEFILE ${STM32CUBEMX_MAKEFILE})

# -------------------------------- STM32CUBEMX ------------------------------- #
add_library(stm32cubemx INTERFACE)

# --------------------------------- STM32 Define ------------------------------ #
extract_makefile_list("C_DEFS" c_defs "-D" FALSE)
target_compile_definitions(stm32cubemx INTERFACE 
    ${c_defs}
)

# ---------------------------------- Lib Flags -------------------------------- #
extract_makefile_list("LIBS" libs "-l" FALSE)
target_link_libraries(stm32cubemx INTERFACE 
    ${libs}
)

# --------------------------------- STM32 Header ------------------------------ #
extract_makefile_list("C_INCLUDES" c_includes "-I" TRUE)

list(TRANSFORM c_includes PREPEND ${PATH_PREFIX})

target_include_directories(stm32cubemx INTERFACE
    ${c_includes}
)

# --------------------------------- STM32 Source ------------------------------ #
extract_makefile_list("C_SOURCES" c_sources "" FALSE)
extract_makefile_list("ASM_SOURCES" asm_sources "" FALSE)

list(TRANSFORM c_sources PREPEND ${PATH_PREFIX})
list(TRANSFORM asm_sources PREPEND ${PATH_PREFIX})

target_sources(stm32cubemx INTERFACE
    ${c_sources}
    ${asm_sources}
)

# --------------------------------- After Config ------------------------------ #
string(REGEX MATCH "STM32[A-Z][0-9][0-9][0-9]" target_device ${c_defs})
message(STATUS "====================================")
message(STATUS "${target_device} (${CMAKE_BUILD_TYPE})")
message(STATUS "====================================")
